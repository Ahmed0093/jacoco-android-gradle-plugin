plugins {
  id 'groovy'
  id 'maven-publish'
  id 'jacoco'
  id 'pl.allegro.tech.build.axion-release' version '1.3.1'
  id 'com.jfrog.bintray' version '1.3.1'
}

configurations {
  provided
}

repositories {
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.3.10'
  compile gradleApi()

  provided 'com.google.code.findbugs:jsr305:2.0.1'

  testCompile(group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testCompile 'com.android.tools.build:gradle:1.3.1'
}

scmVersion {
  tag {
    prefix = 'jacoco-android-release'
  }
}

group = 'com.dicedmelon.gradle'
version = scmVersion.version

test {
  testLogging {
    exceptionFormat "full"
  }
}

jacocoTestReport {
  reports {
    html.enabled false
    xml.enabled true
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    plugin(MavenPublication) {
      from components.java

      artifact sourcesJar {
        classifier "sources"
      }
    }
  }
}

bintray {
  def credentials = (project.hasProperty('bintray.credentials') ?
      project.property('bintray.credentials') : System.getenv('BINTRAY_CREDENTIALS') ?: '').split(
      ':')
  user = credentials.first()
  key = credentials.last()
  publications = ['plugin']
  pkg {
    repo = 'maven'
    name = "$project.group:$project.name"
    userOrg = 'dicedmelon'
    version {
      gpg {
        sign = true
      }
    }
  }
}
